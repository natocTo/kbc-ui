React = require 'react'

TaskSelectTable = require '../components/TaskSelectTable'

Modal = React.createFactory(require('./../../../../react/common/KbcBootstrap').Modal)
ModalHeader = React.createFactory(require('./../../../../react/common/KbcBootstrap').Modal.Header)
ModalTitle = React.createFactory(require('./../../../../react/common/KbcBootstrap').Modal.Title)
ModalBody = React.createFactory(require('./../../../../react/common/KbcBootstrap').Modal.Body)
ModalFooter = React.createFactory(require('./../../../../react/common/KbcBootstrap').Modal.Footer)
Button = React.createFactory(require('./../../../../react/common/KbcBootstrap').Button)
{Panel} = require('./../../../../react/common/KbcBootstrap')
{div, p, span, i, input, label} = React.DOM

Loader = React.createFactory(require('kbc-react-components').Loader)
Panel  = React.createFactory Panel
ConfirmButtons = require('../../../../react/common/ConfirmButtons').default

OrchestrationsApi = require('../../OrchestrationsApi')

module.exports = React.createClass
  displayName: 'TaskSelect'
  propTypes:
    job: React.PropTypes.object.isRequired
    onChange: React.PropTypes.func.isRequired
    onRun: React.PropTypes.func.isRequired
    onOpen: React.PropTypes.func.isRequired
    isSaving: React.PropTypes.bool.isRequired

  getInitialState: ->
    showModal: false

  close: ->
    @setState
      showModal: false

  open: ->
    @props.onOpen()
    @setState
      showModal: true

  render: ->
    tasks = @props.tasks

    span null,
      @renderOpenButton()
      Modal
        show: @state.showModal
        bsSize: 'large'
        keyboard: false
        onHide: @close
      ,
        ModalHeader closeButton: true,
          ModalTitle null,
            'Retry job'

        ModalBody null,
          p null,
            'You are about to run orchestration again'
          Panel
            header: 'Choose orchestration tasks to run'
            collapsible: true
          ,
            div className: 'row',
              React.createElement TaskSelectTable,
                tasks: tasks
                onTaskUpdate: @_handleTaskUpdate

        ModalFooter null,
          div null,
            div className: 'col-sm-6'
            div className: 'col-sm-6',
              React.createElement ConfirmButtons,
                isSaving: false
                isDisabled: !@_isValid()
                saveLabel: 'Run'
                onCancel: @close
                onSave: @_handleRun

  renderOpenButton: ->
    Button
      onClick: @open
      bsStyle: 'link'
    ,
      if @props.isSaving
        Loader null
      else
        i className: 'fa fa-play'
      ' Job retry'

  _handleRun: (e) ->
    @props.onRun()
    @close()

  _handleTaskUpdate: (updatedTask) ->
    @props.onChange(updatedTask)

  _isValid: (e) ->
    @props.tasks.filter((tasks) ->
      tasks.get('tasks').filter((task) ->
        task.get('active')
      ).count() > 0
    ).count() > 0